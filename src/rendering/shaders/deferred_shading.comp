#version 460

#include "structs.inc.glsl"

layout(local_size_x = 8, local_size_y = 8) in;

layout(set = 0, binding = 0, rgb10_a2) uniform readonly image2D normals;
layout(set = 0, binding = 1, r11f_g11f_b10f) uniform readonly image2D albedo;
layout(set = 0, binding = 2) uniform sampler2D depth;

layout(set = 0, binding = 3, rgba16f) uniform writeonly image2D outLighting;

// Reference: https://johnwhite3d.blogspot.com/2017/10/signed-octahedron-normal-encoding.html
vec3 signedOctDecode(vec3 encodedNormal)
{
    vec3 normal;

    normal.x = (encodedNormal.x - encodedNormal.y);
    normal.y = (encodedNormal.x + encodedNormal.y) - 1.0;
    normal.z = encodedNormal.z * 2.0 - 1.0;
    normal.z = normal.z * (1.0 - abs(normal.x) - abs(normal.y));

    normal = normalize(normal);
    return normal;
}

void main() {
    ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);

    ivec2 size = imageSize(outLighting);
    if (pixelCoords.x > size.x || pixelCoords.y > size.y) {
        return;
    }

    vec4 encodedNormal = imageLoad(normals, pixelCoords);
    vec3 normal = signedOctDecode(encodedNormal.xyw);

    vec3 albedoColor = imageLoad(albedo, pixelCoords).xyz;
    float depthValue = texelFetch(depth, pixelCoords, 0).x;

    // TODO: pass lights in uniform buffer
    vec3 lightDir = normalize(vec3(0.5, 1.0, -0.3));
    float diffuse = max(dot(normal, lightDir), 0.0);
    vec3 lighting = albedoColor * diffuse;

    lighting += albedoColor * 0.1; // ambient

    imageStore(outLighting, pixelCoords, vec4(lighting, 1.0));
}
